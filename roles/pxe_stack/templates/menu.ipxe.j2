#!ipxe

# Set graphics mode and configure colours
#console --x 200 --y 200 --picture http://10.10.0.1/texture.png #--left 32 --right 32 --top 32 --bottom 48
#console --x 200 --y 200 --picture riddick.png

###################### MAIN MENU ####################################

# Some menu defaults
set menu-timeout 10000
set submenu-timeout ${menu-timeout}
isset ${menu-default} || set menu-default bootdisk
set esc:hex 1b
set bold ${esc:string}[1m
set orange ${esc:string}[33;0m
set yellow ${esc:string}[33;1m
set cyan ${esc:string}[36;1m
set resetfg ${esc:string}[39m
set resetbg ${esc:string}[49m
set resetbold ${esc:string}[22m
set reset ${esc:string}[0m

# Figure out if client is 64-bit capable
cpuid --ext 29 && set arch x86_64 || set arch x86
cpuid --ext 29 && set archb 64 || set archb 32
cpuid --ext 29 && set archl x86_64 || set archl i386

:start
menu BlueBanquise iPXE boot menu for ${manufacturer} ${product} (${archb}bit)
item --gap                Main boot
item bootosdeploy         Deploy OS (This will deploy/redeploy operating system)
item bootdisk             Boot on disk (This will boot on local disk)
item --gap                Tools
item memtest              Memtest (To check Memory)
item --gap                Other
item startshell           iPXE shell (Go to an iPXE shell)
choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
set menu-timeout 0
goto ${selected}

:startshell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

############ MAIN MENU ITEMS ############

:bootosdeploy

chain http://${next-server}/preboot_execution_environment/nodes/${hostname}_deploy_os.ipxe || shell

:bootdisk

iseq ${platform} efi && goto bootdisk-efi || goto bootdisk-legacy

    :bootdisk-legacy
    echo Booting on disk in legacy bios way
    sleep 2
    sanboot --no-describe --drive 0x80 || shell
    exit

    :bootdisk-efi
    echo Booting on disk in EFI way
    exit 1

:memtest
echo Booting Memtest86+
boot http://${next-server}/preboot_execution_environment/bin/memtest86+-5.01.bin || shell



